import groovy.io.FileType

plugins {
    id 'ru.vyarus.quality' version '3.2.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'

def suitesDir = 'src/test/resources/suites/'

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'joda-time', name: 'joda-time', version: '2.9.9'
    compile group: 'org.testng', name: 'testng', version: '6.14.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.3'
}

test {
    useTestNG() {
        // Add test suites
        def suitesList = []
        def dir = new File(suitesDir)
        dir.eachFileRecurse(FileType.FILES) { file -> suitesList << file }
        suitesList.each { options.suites(it.path) }

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

quality {
    checkstyle = true
    pmd = true
    findbugs = true
    codenarc = true
    spotbugs = true
}

check.dependsOn jacocoTestReport